10 REM TRIGONOMETRY 3D DEMO
20 GOSUB 1000
30 REM X1=2:GOSUB 1100
40 DIM D(1,255):FOR L=0 TO 255:D(0,L)=-1:D(1,L)=-1:NEXT
50 PI=3.14
60 FOR Y=-180 TO 180 STEP 4:FOR X=-180 TO 180 STEP 4
80 R=PI/180*SQR(X*X+Y*Y):Z=100*COS(R)-30*COS(3*R)
100 V=INT(116+X/2+(16-Y/2)/2):W=INT((130-Y/2-Z)/2)
120 IF (V<0)+(V>255) THEN GOTO 160
130 IF D(0,V)=-1 THEN GOTO 500
140 IF W<=D(0,V) THEN GOTO 700
150 IF W>=D(1,V) THEN GOTO 800
160 NEXT X:NEXT Y:END
500 IF V=0 THEN GOTO 600
510 IF D(0,V-1)=-1 THEN GOTO 600
520 IF D(1,V+1)=-1 THEN GOTO 600
530 D(0,V)=INT((D(0,V-1)+D(0,V+1))/2)
540 D(1,V)=INT((D(1,V-1)+D(1,V+1))/2)
550 GOSUB 900:GOTO 160
600 D(0,V)=W:D(1,V)=W:GOSUB 900:GOTO 160
700 GOSUB 900:D(0,V)=W:IF D(1,V)=-1 THEN D(1,V)=W
710 GOTO 160
800 GOSUB 900:D(1,V)=W:IF D(0,V)=-1 THEN D(0,V)=W
810 GOTO 160
900 X1=V*3.5+30:Y1=600-W*3.5:GOSUB 2100:RETURN
1000 PRINT CHR$(&H1B);CHR$(&H0C);
1010 PRINT CHR$(&H0D);CHR$(&H0A);:RETURN
1100 REM TEKCOLOR(X1)
1110 PRINT CHR$(&H1B);CHR$(&H5B);CHR$(&H33);CHR$(&H30+X1);
1111 PRINT CHR$(&H6D);:RETURN
2100 REM TEKDOT(X1,Y1)
2101 PRINT X1, Y1
2110 PRINT CHR$(&H1D);
2120 PRINT CHR$(&H20+(Y1/32 AND &H1F));CHR$(&H60+(Y1 AND &H1F));
2140 PRINT CHR$(&H20+(X1/32 AND &H1F));CHR$(&H40+(X1 AND &H1F));
2160 PRINT CHR$(&H20+(Y1/32 AND &H1F));CHR$(&H60+(Y1 AND &H1F));
2180 PRINT CHR$(&H20+(X1/32 AND &H1F));CHR$(&H40+(X1 AND &H1F)):RETURN
